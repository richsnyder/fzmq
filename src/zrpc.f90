MODULE ZRPC

  USE :: ZDATA
  IMPLICIT NONE

  TYPE :: ZFUNCTION_WRAPPER_T
    INTEGER(KIND = C_INT) :: ID
    PROCEDURE(ZFUNCTION_T), POINTER, NOPASS :: APPLY
  END TYPE ZFUNCTION_WRAPPER_T

  TYPE :: ZCLIENT_T
    PRIVATE
      TYPE(C_PTR) :: CONTEXT_
      TYPE(C_PTR) :: SOCKET_
      INTEGER(KIND = C_INT) :: STATE_
    CONTAINS
      PROCEDURE :: CLOSE => ZCLIENT_CLOSE
      PROCEDURE :: CONNECT => ZCLIENT_CONNECT
      PROCEDURE :: RECV => ZCLIENT_RECV
      PROCEDURE :: SEND => ZCLIENT_SEND

      PROCEDURE :: BAD => ZCLIENT_BAD
      PROCEDURE :: GOOD => ZCLIENT_GOOD
  END TYPE ZCLIENT_T

  TYPE :: ZSERVER_T
    PRIVATE
      TYPE(C_PTR) :: CONTEXT_
      TYPE(C_PTR) :: SOCKET_
      INTEGER(KIND = C_INT) :: STATE_
      INTEGER(KIND = C_INT) :: NFUNCTIONS_
      TYPE(ZFUNCTION_WRAPPER_T), DIMENSION(:), ALLOCATABLE :: FUNCTIONS_
    CONTAINS
      PROCEDURE :: BIND => ZSERVER_BIND
      PROCEDURE :: CLOSE => ZSERVER_CLOSE
      PROCEDURE :: REGISTER => ZSERVER_REGISTER

      PROCEDURE :: SERVE => ZSERVER_SERVE
      PROCEDURE :: SERVE_N => ZSERVER_SERVE_N
      PROCEDURE :: SERVE_ONCE => ZSERVER_SERVE_ONCE

      PROCEDURE :: BAD => ZSERVER_BAD
      PROCEDURE :: GOOD => ZSERVER_GOOD
  END TYPE ZSERVER_T

  ! STATE FLAGS
  INTEGER, PARAMETER :: ZCLIENT_BADBIT = 1
  INTEGER, PARAMETER :: ZSERVER_BADBIT = 1

  INTERFACE ZSERVER_T

    MODULE PROCEDURE ZSERVER_INIT
    MODULE PROCEDURE ZSERVER_INIT_SIZE

  END INTERFACE ZSERVER_T

  ABSTRACT INTERFACE

    FUNCTION ZFUNCTION_T(REQUEST) RESULT(REPLY)
      IMPORT :: ZDATA_T
      IMPLICIT NONE
      TYPE(ZDATA_T), INTENT(INOUT) :: REQUEST
      TYPE(ZDATA_T) :: REPLY
    END FUNCTION ZFUNCTION_T

  END INTERFACE

CONTAINS

  FUNCTION ZCLIENT_BAD(ZCLIENT) RESULT(FLAG)

    IMPLICIT NONE
    CLASS(ZCLIENT_T), INTENT(IN) :: ZCLIENT
    LOGICAL :: FLAG

    FLAG = BTEST(ZCLIENT%STATE_, ZCLIENT_BADBIT)

  END FUNCTION ZCLIENT_BAD

  SUBROUTINE ZCLIENT_CLOSE(ZCLIENT)

    IMPLICIT NONE
    CLASS(ZCLIENT_T), INTENT(INOUT) :: ZCLIENT

    INTEGER(KIND = C_INT) :: CODE

    CODE = ZMQ_CLOSE(ZCLIENT%SOCKET_)
    CODE = ZMQ_CTX_TERM(ZCLIENT%CONTEXT_)
    ZCLIENT%CONTEXT_ = C_NULL_PTR
    ZCLIENT%SOCKET_ = C_NULL_PTR
    ZCLIENT%STATE_ = 0

  END SUBROUTINE ZCLIENT_CLOSE

  SUBROUTINE ZCLIENT_CONNECT(ZCLIENT, ENDPOINT, SOCKET_TYPE)

    IMPLICIT NONE
    CLASS(ZCLIENT_T), INTENT(INOUT) :: ZCLIENT
    CHARACTER(KIND = C_CHAR, LEN = *), INTENT(IN) :: ENDPOINT
    INTEGER, INTENT(IN) :: SOCKET_TYPE

    INTEGER(KIND = C_INT) :: CODE

    ZCLIENT%CONTEXT_ = ZMQ_CTX_NEW()
    ZCLIENT%SOCKET_ = ZMQ_SOCKET(ZCLIENT%CONTEXT_, SOCKET_TYPE)
    ZCLIENT%STATE_ = 0
    CODE = ZMQ_CONNECT(ZCLIENT%SOCKET_, ENDPOINT)
    IF (CODE .LT. 0) THEN
      ZCLIENT%STATE_ = IBSET(ZCLIENT%STATE_, ZCLIENT_BADBIT)
    END IF

  END SUBROUTINE ZCLIENT_CONNECT

  FUNCTION ZCLIENT_GOOD(ZCLIENT) RESULT(FLAG)

    IMPLICIT NONE
    CLASS(ZCLIENT_T), INTENT(IN) :: ZCLIENT
    LOGICAL :: FLAG

    FLAG = .NOT. BTEST(ZCLIENT%STATE_, ZCLIENT_BADBIT)

  END FUNCTION ZCLIENT_GOOD

  FUNCTION ZCLIENT_INIT(ENDPOINT, SOCKET_TYPE) RESULT(ZCLIENT)

    IMPLICIT NONE
    CHARACTER(KIND = C_CHAR, LEN = *), INTENT(IN) :: ENDPOINT
    INTEGER, INTENT(IN) :: SOCKET_TYPE
    TYPE(ZCLIENT_T) :: ZCLIENT

    CALL ZCLIENT%CONNECT(ENDPOINT, SOCKET_TYPE)

  END FUNCTION ZCLIENT_INIT

  FUNCTION ZCLIENT_RECV(ZCLIENT, REPLY) RESULT(NBYTES)

    IMPLICIT NONE
    CLASS(ZCLIENT_T), INTENT(INOUT) :: ZCLIENT
    TYPE(ZDATA_T), INTENT(INOUT) :: REPLY
    INTEGER(KIND = C_INT) :: NBYTES

    NBYTES = REPLY%RECV(ZCLIENT%SOCKET_, 0)

  END FUNCTION ZCLIENT_RECV

  FUNCTION ZCLIENT_SEND(ZCLIENT, REQUEST) RESULT(NBYTES)

    IMPLICIT NONE
    CLASS(ZCLIENT_T), INTENT(INOUT) :: ZCLIENT
    TYPE(ZDATA_T), INTENT(INOUT) :: REQUEST
    INTEGER(KIND = C_INT) :: NBYTES

    NBYTES = REQUEST%SEND(ZCLIENT%SOCKET_, 0)

  END FUNCTION ZCLIENT_SEND

  FUNCTION ZSERVER_BAD(ZSERVER) RESULT(FLAG)

    IMPLICIT NONE
    CLASS(ZSERVER_T), INTENT(IN) :: ZSERVER
    LOGICAL :: FLAG

    FLAG = BTEST(ZSERVER%STATE_, ZSERVER_BADBIT)

  END FUNCTION ZSERVER_BAD

  SUBROUTINE ZSERVER_BIND(ZSERVER, ENDPOINT, SOCKET_TYPE)

    IMPLICIT NONE
    CLASS(ZSERVER_T), INTENT(INOUT) :: ZSERVER
    CHARACTER(KIND = C_CHAR, LEN = *), INTENT(IN) :: ENDPOINT
    INTEGER, INTENT(IN) :: SOCKET_TYPE

    INTEGER(KIND = C_INT) :: CODE

    ZSERVER%CONTEXT_ = ZMQ_CTX_NEW()
    ZSERVER%SOCKET_ = ZMQ_SOCKET(ZSERVER%CONTEXT_, SOCKET_TYPE)
    CODE = ZMQ_BIND(ZSERVER%SOCKET_, ENDPOINT)
    IF (CODE .LT. 0) THEN
      ZSERVER%STATE_ = IBSET(ZSERVER%STATE_, ZSERVER_BADBIT)
    END IF

  END SUBROUTINE ZSERVER_BIND

  SUBROUTINE ZSERVER_CLOSE(ZSERVER)

    IMPLICIT NONE
    CLASS(ZSERVER_T), INTENT(INOUT) :: ZSERVER

    INTEGER(KIND = C_INT) :: CODE

    CODE = ZMQ_CLOSE(ZSERVER%SOCKET_)
    CODE = ZMQ_CTX_TERM(ZSERVER%CONTEXT_)
    DEALLOCATE(ZSERVER%FUNCTIONS_)
    ZSERVER%CONTEXT_ = C_NULL_PTR
    ZSERVER%SOCKET_ = C_NULL_PTR
    ZSERVER%STATE_ = 0
    ZSERVER%NFUNCTIONS_ = 0

  END SUBROUTINE ZSERVER_CLOSE

  FUNCTION ZSERVER_GOOD(ZSERVER) RESULT(FLAG)

    IMPLICIT NONE
    CLASS(ZSERVER_T), INTENT(IN) :: ZSERVER
    LOGICAL :: FLAG

    FLAG = .NOT. BTEST(ZSERVER%STATE_, ZSERVER_BADBIT)

  END FUNCTION ZSERVER_GOOD

  FUNCTION ZSERVER_INIT() RESULT(ZSERVER)

    IMPLICIT NONE
    TYPE(ZSERVER_T) :: ZSERVER

    ZSERVER%CONTEXT_ = C_NULL_PTR
    ZSERVER%SOCKET_ = C_NULL_PTR
    ZSERVER%STATE_ = 0
    ZSERVER%NFUNCTIONS_ = 0
    ALLOCATE(ZSERVER%FUNCTIONS_(16))

  END FUNCTION ZSERVER_INIT

  FUNCTION ZSERVER_INIT_SIZE(SIZE) RESULT(ZSERVER)

    IMPLICIT NONE
    INTEGER(KIND = C_INT), INTENT(IN) :: SIZE
    TYPE(ZSERVER_T) :: ZSERVER

    ZSERVER%CONTEXT_ = C_NULL_PTR
    ZSERVER%SOCKET_ = C_NULL_PTR
    ZSERVER%STATE_ = 0
    ZSERVER%NFUNCTIONS_ = 0
    ALLOCATE(ZSERVER%FUNCTIONS_(SIZE))

  END FUNCTION ZSERVER_INIT_SIZE

  SUBROUTINE ZSERVER_REGISTER(ZSERVER, ID, FN)

    IMPLICIT NONE
    CLASS(ZSERVER_T), INTENT(INOUT) :: ZSERVER
    INTEGER(KIND = C_INT), INTENT(IN) :: ID
    PROCEDURE(ZFUNCTION_T), POINTER, INTENT(IN) :: FN

    IF (ZSERVER%NFUNCTIONS_ .LT. 16) THEN
      ZSERVER%NFUNCTIONS_ = ZSERVER%NFUNCTIONS_ + 1
      ZSERVER%FUNCTIONS_(ZSERVER%NFUNCTIONS_)%ID = ID
      ZSERVER%FUNCTIONS_(ZSERVER%NFUNCTIONS_)%APPLY => FN
    ELSE
      ZSERVER%STATE_ = IBSET(ZSERVER%STATE_, ZSERVER_BADBIT)
    END IF

  END SUBROUTINE ZSERVER_REGISTER

  SUBROUTINE ZSERVER_SERVE(ZSERVER)

    IMPLICIT NONE
    CLASS(ZSERVER_T), INTENT(INOUT) :: ZSERVER

    DO WHILE (.TRUE.)
      CALL ZSERVER_SERVE_ONCE(ZSERVER)
    END DO

  END SUBROUTINE ZSERVER_SERVE

  SUBROUTINE ZSERVER_SERVE_N(ZSERVER, COUNT)

    IMPLICIT NONE
    CLASS(ZSERVER_T), INTENT(INOUT) :: ZSERVER
    INTEGER(KIND = C_INT), INTENT(IN) :: COUNT

    INTEGER(KIND = C_INT) :: N

    DO N = 1, COUNT
      CALL ZSERVER_SERVE_ONCE(ZSERVER)
    END DO

  END SUBROUTINE ZSERVER_SERVE_N

  SUBROUTINE ZSERVER_SERVE_ONCE(ZSERVER)

    IMPLICIT NONE
    CLASS(ZSERVER_T), INTENT(INOUT) :: ZSERVER

    TYPE(ZDATA_T) :: REQUEST
    TYPE(ZDATA_T) :: REPLY
    INTEGER(KIND = C_INT) :: N
    INTEGER(KIND = C_INT) :: ID
    INTEGER(KIND = C_INT) :: NBYTES

    REQUEST = ZDATA_T()
    NBYTES = REQUEST%RECV(ZSERVER%SOCKET_, 0)
    ID = REQUEST%GET_INT()

    DO N = 1, ZSERVER%NFUNCTIONS_
      IF (ZSERVER%FUNCTIONS_(N)%ID .EQ. ID) THEN
        REPLY = ZSERVER%FUNCTIONS_(N)%APPLY(REQUEST)
      END IF
    END DO

    CALL REQUEST%CLOSE()
    NBYTES = REPLY%SEND(ZSERVER%SOCKET_, 0)

  END SUBROUTINE ZSERVER_SERVE_ONCE

END MODULE ZRPC
