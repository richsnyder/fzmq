PROGRAM TEST_SEND_CONST

USE ZMQ
USE ASSERT
IMPLICIT NONE

TYPE(C_PTR) :: CONTEXT
TYPE(C_PTR) :: SOCKET
INTEGER(KIND = C_INT) :: CODE
INTEGER(KIND = C_INT) :: NBYTES
INTEGER(KIND = C_INT), TARGET :: VALUE
INTEGER(KIND = C_INT64_T), TARGET :: VALUE64
CHARACTER(KIND = C_CHAR, LEN = 16), TARGET :: STR

CALL OMP_SET_DYNAMIC(0)
CALL OMP_SET_NUM_THREADS(2)

CONTEXT = ZMQ_CTX_NEW()

!$OMP PARALLEL SHARED(CONTEXT), PRIVATE(SOCKET,CODE,VALUE,VALUE64,STR)
!$OMP SECTIONS

!$OMP SECTION

  SOCKET = ZMQ_SOCKET(CONTEXT, ZMQ_PAIR)
  CODE = ZMQ_CONNECT(SOCKET, 'inproc://fzmq')
  CALL ASSERT_EQUALS(0, CODE)

  NBYTES = ZMQ_SEND_CONST(SOCKET, 1234567890_C_INT, 0)
  CALL ASSERT_EQUALS(INT(C_SIZEOF(VALUE), KIND = C_INT), NBYTES)

  NBYTES = ZMQ_SEND_CONST(SOCKET, 2345678901_C_INT64_T, 0)
  CALL ASSERT_EQUALS(INT(C_SIZEOF(VALUE64), KIND = C_INT), NBYTES)

  NBYTES = ZMQ_SEND_CONST(SOCKET, 'Hello, world!' // C_NULL_CHAR, 0)
  CALL ASSERT_EQUALS(14, NBYTES)

  CODE = ZMQ_CLOSE(SOCKET)
  CALL ASSERT_EQUALS(0, CODE)

!$OMP SECTION

  SOCKET = ZMQ_SOCKET(CONTEXT, ZMQ_PAIR)
  CODE = ZMQ_BIND(SOCKET, 'inproc://fzmq')
  CALL ASSERT_EQUALS(0, CODE)

  NBYTES = ZMQ_RECV(SOCKET, C_LOC(VALUE), C_SIZEOF(VALUE), 0)
  CALL ASSERT_EQUALS(INT(C_SIZEOF(VALUE), KIND = C_INT), NBYTES)
  CALL ASSERT_EQUALS(1234567890_C_INT, VALUE)

  NBYTES = ZMQ_RECV(SOCKET, C_LOC(VALUE64), C_SIZEOF(VALUE64), 0)
  CALL ASSERT_EQUALS(INT(C_SIZEOF(VALUE64), KIND = C_INT), NBYTES)
  CALL ASSERT_EQUALS(2345678901_C_INT64_T, VALUE64)

  NBYTES = ZMQ_RECV(SOCKET, STR, 0)
  CALL ASSERT_EQUALS(14, NBYTES)
  CALL ASSERT_TRUE(TRIM(STR) == 'Hello, world!')

  CODE = ZMQ_CLOSE(SOCKET)
  CALL ASSERT_EQUALS(0, CODE)

!$OMP END SECTIONS
!$OMP END PARALLEL

CODE = ZMQ_CTX_TERM(CONTEXT)
CALL ASSERT_EQUALS(0, CODE)

END PROGRAM
