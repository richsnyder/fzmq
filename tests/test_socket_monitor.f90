PROGRAM TEST_SEND_RECV

USE, INTRINSIC :: ISO_C_BINDING
USE ZMQ
USE ASSERT
IMPLICIT NONE

TYPE(C_PTR) :: CONTEXT
TYPE(C_PTR) :: SOCKET
TYPE(C_PTR) :: MONITOR
INTEGER(KIND = C_INT) :: CODE
INTEGER(KIND = C_INT) :: NBYTES
INTEGER(KIND = C_INT) :: NEVENTS
INTEGER(KIND = C_SHORT) :: EVENT_NUMBER
TYPE(ZMQ_POLLITEM_T), DIMENSION(2) :: ITEMS
CHARACTER(KIND = C_CHAR, LEN = 256) :: BUFFER

CALL OMP_SET_DYNAMIC(0)
CALL OMP_SET_NUM_THREADS(2)

CONTEXT = ZMQ_CTX_NEW()

!$OMP PARALLEL SHARED(CONTEXT), PRIVATE(SOCKET,MONITOR,CODE,NBYTES,NEVENTS,EVENT_NUMBER,ITEMS,BUFFER)
!$OMP SECTIONS

!$OMP SECTION

  SOCKET = ZMQ_SOCKET(CONTEXT, ZMQ_REQ)
  CODE = ZMQ_CONNECT(SOCKET, 'ipc:///tmp/fzmq')
  CALL ASSERT_EQUALS(0, CODE)

  CODE = ZMQ_POLL(ITEMS, 0, 100_8)
  NBYTES = ZMQ_SEND(SOCKET, 'Echo', 0)
  NBYTES = ZMQ_RECV(SOCKET, BUFFER, 0)

  CODE = ZMQ_CLOSE(SOCKET)
  CALL ASSERT_EQUALS(0, CODE)

!$OMP SECTION

  SOCKET = ZMQ_SOCKET(CONTEXT, ZMQ_REP)
  CODE = ZMQ_BIND(SOCKET, 'ipc:///tmp/fzmq')
  CALL ASSERT_EQUALS(0, CODE)

  MONITOR = ZMQ_SOCKET(CONTEXT, ZMQ_PAIR)
  CODE = ZMQ_SOCKET_MONITOR(SOCKET, 'inproc://fzmq-monitor', ZMQ_EVENT_ACCEPTED)
  CALL ASSERT_EQUALS(0, CODE)
  CODE = ZMQ_CONNECT(MONITOR, 'inproc://fzmq-monitor')
  CALL ASSERT_EQUALS(0, CODE)

  ITEMS(1)%SOCKET = SOCKET
  ITEMS(2)%SOCKET = MONITOR
  ITEMS(1)%EVENTS = ZMQ_POLLIN
  ITEMS(2)%EVENTS = ZMQ_POLLIN

  NEVENTS = 0
  DO WHILE(NEVENTS < 2 .AND. ZMQ_POLL(ITEMS, 2, 200_8) > 0)
    IF (IAND(ITEMS(1)%REVENTS, ZMQ_POLLIN) /= 0) THEN
      NEVENTS = NEVENTS + 1
      NBYTES = ZMQ_RECV(SOCKET, BUFFER, 0)
      NBYTES = ZMQ_SEND(SOCKET, BUFFER(1:NBYTES), 0)
    ELSE IF (IAND(ITEMS(2)%REVENTS, ZMQ_POLLIN) /= 0) THEN
      NEVENTS = NEVENTS + 1
      NBYTES = ZMQ_RECV(MONITOR, BUFFER, 0)
      EVENT_NUMBER = TRANSFER(BUFFER(1:2), EVENT_NUMBER)
      EVENT_NUMBER = IAND(EVENT_NUMBER, ZMQ_EVENT_ACCEPTED)
      NBYTES = ZMQ_RECV(MONITOR, BUFFER, 0)
      CALL ASSERT_EQUALS(ZMQ_EVENT_ACCEPTED, INT(EVENT_NUMBER, KIND = C_INT))
    END IF
  END DO

  CALL ASSERT_EQUALS(2, NEVENTS)

  CODE = ZMQ_CLOSE(SOCKET)
  CALL ASSERT_EQUALS(0, CODE)
  CODE = ZMQ_CLOSE(MONITOR)
  CALL ASSERT_EQUALS(0, CODE)

!$OMP END SECTIONS
!$OMP END PARALLEL

CODE = ZMQ_CTX_TERM(CONTEXT)
CALL ASSERT_EQUALS(0, CODE)

END PROGRAM
