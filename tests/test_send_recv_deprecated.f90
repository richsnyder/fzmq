PROGRAM TEST_SEND_RECV_DEPRECATED

USE ZMQ
USE ASSERT
IMPLICIT NONE

TYPE(C_PTR) :: CONTEXT
TYPE(C_PTR) :: SOCKET
INTEGER(KIND = C_INT) :: CODE
INTEGER(KIND = C_INT) :: VALUE

CALL OMP_SET_DYNAMIC(0)
CALL OMP_SET_NUM_THREADS(2)

CONTEXT = ZMQ_INIT(1)

!$OMP PARALLEL SHARED(CONTEXT), PRIVATE(SOCKET,CODE,VALUE)
!$OMP SECTIONS

!$OMP SECTION

  SOCKET = ZMQ_SOCKET(CONTEXT, ZMQ_PAIR)
  CODE = ZMQ_CONNECT(SOCKET, 'inproc://fzmq')
  CALL ASSERT_EQUALS(0, CODE)

  CALL SEND(SOCKET, 123)

  CODE = ZMQ_CLOSE(SOCKET)
  CALL ASSERT_EQUALS(0, CODE)

!$OMP SECTION

  SOCKET = ZMQ_SOCKET(CONTEXT, ZMQ_PAIR)
  CODE = ZMQ_BIND(SOCKET, 'inproc://fzmq')
  CALL ASSERT_EQUALS(0, CODE)

  CALL RECV(SOCKET, VALUE)
  CALL ASSERT_EQUALS(123, VALUE)

  CODE = ZMQ_CLOSE(SOCKET)
  CALL ASSERT_EQUALS(0, CODE)

!$OMP END SECTIONS
!$OMP END PARALLEL

CODE = ZMQ_TERM(CONTEXT)
CALL ASSERT_EQUALS(0, CODE)

CONTAINS

  SUBROUTINE SEND(SOCKET, VALUE)
    TYPE(C_PTR), INTENT(INOUT) :: SOCKET
    INTEGER(KIND = C_INT), INTENT(IN) :: VALUE

    TYPE(C_PTR) :: DATA
    TYPE(ZMQ_MSG_T) :: MESSAGE
    INTEGER(KIND = C_INT) :: CODE
    INTEGER(KIND = C_INT) :: NBYTES
    INTEGER(KIND = C_SIZE_T) :: SIZE
    CHARACTER(KIND = C_CHAR, LEN = :), POINTER :: BUFFER
    CHARACTER(KIND = C_CHAR, LEN = :), POINTER :: RANGE

    SIZE = C_SIZEOF(VALUE)
    CODE = ZMQ_MSG_INIT_SIZE(MESSAGE, SIZE)
    CALL ASSERT_EQUALS(0, CODE)

    DATA = ZMQ_MSG_DATA(MESSAGE)
    CALL C_F_POINTER(DATA, BUFFER)
    RANGE => BUFFER(1:SIZE)
    RANGE = TRANSFER(VALUE, RANGE)

    NBYTES = ZMQ_SENDMSG(SOCKET, MESSAGE, 0)
    CALL ASSERT_EQUALS(INT(SIZE, KIND = C_INT), NBYTES)
  END SUBROUTINE

  SUBROUTINE RECV(SOCKET, VALUE)
    TYPE(C_PTR), INTENT(INOUT) :: SOCKET
    INTEGER(KIND = C_INT), INTENT(OUT) :: VALUE

    TYPE(C_PTR) :: DATA
    TYPE(ZMQ_MSG_T) :: MESSAGE
    INTEGER(KIND = C_INT) :: CODE
    INTEGER(KIND = C_INT) :: NBYTES
    INTEGER(KIND = C_SIZE_T) :: SIZE
    CHARACTER(KIND = C_CHAR, LEN = :), POINTER :: BUFFER
    CHARACTER(KIND = C_CHAR, LEN = :), POINTER :: RANGE

    SIZE = C_SIZEOF(VALUE)
    CODE = ZMQ_MSG_INIT(MESSAGE)
    CALL ASSERT_EQUALS(0, CODE)

    NBYTES = ZMQ_RECVMSG(SOCKET, MESSAGE, 0)
    CALL ASSERT_EQUALS(INT(SIZE, KIND = C_INT), NBYTES)

    DATA = ZMQ_MSG_DATA(MESSAGE)
    CALL C_F_POINTER(DATA, BUFFER)
    RANGE => BUFFER(1:SIZE)
    VALUE = TRANSFER(RANGE, VALUE)

    CODE = ZMQ_MSG_CLOSE(MESSAGE)
    CALL ASSERT_EQUALS(0, CODE)
  END SUBROUTINE

END PROGRAM
