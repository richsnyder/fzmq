PROGRAM TEST_SOCKET_OPTIONS

USE ZMQ
USE ASSERT
IMPLICIT NONE

TYPE(C_PTR) :: CONTEXT
TYPE(C_PTR) :: SOCKET
INTEGER(KIND = C_INT) :: CODE
INTEGER(KIND = C_SIZE_T) :: OPTION_LEN

INTEGER(KIND = C_INT) :: EXPECTED_I32
INTEGER(KIND = C_INT) :: ACTUAL_I32
INTEGER(KIND = C_INT64_T) :: EXPECTED_I64
INTEGER(KIND = C_INT64_T) :: ACTUAL_I64
CHARACTER(KIND = C_CHAR, LEN = 256), TARGET :: EXPECTED_STR
CHARACTER(KIND = C_CHAR, LEN = 256), TARGET :: ACTUAL_STR

CONTEXT = ZMQ_CTX_NEW()

SOCKET = ZMQ_SOCKET(CONTEXT, ZMQ_REQ)

! Binary option

EXPECTED_STR = 'fzmq'
OPTION_LEN = 4_C_SIZE_T
CODE = ZMQ_SETSOCKOPT(SOCKET, ZMQ_IDENTITY, C_LOC(EXPECTED_STR), OPTION_LEN)
CALL ASSERT_EQUALS(0, CODE)

ACTUAL_STR = ''
OPTION_LEN = 256_C_SIZE_T
CODE = ZMQ_GETSOCKOPT(SOCKET, ZMQ_IDENTITY, C_LOC(ACTUAL_STR), OPTION_LEN)
CALL ASSERT_EQUALS(0, CODE)
CALL ASSERT_EQUALS(4_C_SIZE_T, OPTION_LEN)
CALL ASSERT_TRUE(EXPECTED_STR(1:4) == ACTUAL_STR(1:4))

! Integer option

EXPECTED_I32 = 500_C_INT
CODE = ZMQ_SETSOCKOPT(SOCKET, ZMQ_BACKLOG, EXPECTED_I32)
CALL ASSERT_EQUALS(0, CODE)

ACTUAL_I32 = 0_C_INT
CODE = ZMQ_GETSOCKOPT(SOCKET, ZMQ_BACKLOG, ACTUAL_I32)
CALL ASSERT_EQUALS(0, CODE)
CALL ASSERT_EQUALS(EXPECTED_I32, ACTUAL_I32)

! 64-bit integer option

EXPECTED_I64 = 1024_C_INT64_T
CODE = ZMQ_SETSOCKOPT(SOCKET, ZMQ_MAXMSGSIZE, EXPECTED_I64)
CALL ASSERT_EQUALS(0, CODE)

ACTUAL_I64 = 0_C_INT64_T
CODE = ZMQ_GETSOCKOPT(SOCKET, ZMQ_MAXMSGSIZE, ACTUAL_I64)
CALL ASSERT_EQUALS(0, CODE)
CALL ASSERT_EQUALS(EXPECTED_I64, ACTUAL_I64)

! Character string option

EXPECTED_STR = 'username'
CODE = ZMQ_SETSOCKOPT(SOCKET, ZMQ_PLAIN_USERNAME, EXPECTED_STR)
CALL ASSERT_EQUALS(0, CODE)

ACTUAL_STR = ''
CODE = ZMQ_GETSOCKOPT(SOCKET, ZMQ_PLAIN_USERNAME, ACTUAL_STR)
CALL ASSERT_EQUALS(0, CODE)
CALL ASSERT_TRUE(EXPECTED_STR == ACTUAL_STR)

CODE = ZMQ_CLOSE(SOCKET)
CALL ASSERT_EQUALS(0, CODE)

CODE = ZMQ_CTX_TERM(CONTEXT)
CALL ASSERT_EQUALS(0, CODE)

END PROGRAM
