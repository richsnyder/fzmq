PROGRAM TEST_POLL

USE, INTRINSIC :: ISO_C_BINDING
USE ZMQ
USE ASSERT
IMPLICIT NONE

TYPE(C_PTR) :: CONTEXT
TYPE(C_PTR) :: SOCKET
INTEGER(KIND = C_INT) :: CODE
INTEGER(KIND = C_INT) :: VALUE
TYPE(ZMQ_POLLITEM_T), DIMENSION(1) :: ITEMS

CALL OMP_SET_DYNAMIC(0)
CALL OMP_SET_NUM_THREADS(2)

CONTEXT = ZMQ_CTX_NEW()

!$OMP PARALLEL SHARED(CONTEXT), PRIVATE(SOCKET,CODE,VALUE,ITEMS)
!$OMP SECTIONS

!$OMP SECTION

  SOCKET = ZMQ_SOCKET(CONTEXT, ZMQ_PAIR)
  CODE = ZMQ_CONNECT(SOCKET, 'inproc://fzmq')
  CALL ASSERT_EQUALS(0, CODE)

  CALL SEND(SOCKET, 123)

  CODE = ZMQ_CLOSE(SOCKET)
  CALL ASSERT_EQUALS(0, CODE)

!$OMP SECTION

  SOCKET = ZMQ_SOCKET(CONTEXT, ZMQ_PAIR)
  CODE = ZMQ_BIND(SOCKET, 'inproc://fzmq')
  CALL ASSERT_EQUALS(0, CODE)

  VALUE = 0
  ITEMS(1)%SOCKET = SOCKET
  ITEMS(1)%EVENTS = ZMQ_POLLIN

  DO WHILE (ZMQ_POLL(ITEMS, 1, 1000_8) > 0)
    IF (IAND(ITEMS(1)%REVENTS, ZMQ_POLLIN) /= 0) THEN
      CALL RECV(SOCKET, VALUE)
      EXIT
    END IF
  END DO

  CALL ASSERT_EQUALS(123, VALUE)

  CODE = ZMQ_CLOSE(SOCKET)
  CALL ASSERT_EQUALS(0, CODE)

!$OMP END SECTIONS
!$OMP END PARALLEL

CODE = ZMQ_CTX_TERM(CONTEXT)
CALL ASSERT_EQUALS(0, CODE)

CONTAINS

  SUBROUTINE SEND(SOCKET, VALUE)
    TYPE(C_PTR), INTENT(INOUT) :: SOCKET
    INTEGER(KIND = C_INT), INTENT(IN) :: VALUE

    INTEGER(KIND = C_INT) :: CODE
    INTEGER(KIND = C_INT) :: NBYTES
    INTEGER(KIND = C_SIZE_T) :: SIZE
    CHARACTER(KIND = C_CHAR, LEN = :), TARGET, ALLOCATABLE :: BUFFER
    CHARACTER(KIND = C_CHAR, LEN = :), POINTER :: RANGE

    SIZE = C_SIZEOF(VALUE)
    ALLOCATE(CHARACTER(KIND = C_CHAR, LEN = SIZE) :: BUFFER)
    RANGE => BUFFER(1:SIZE)
    RANGE = TRANSFER(VALUE, RANGE)

    NBYTES = ZMQ_SEND(SOCKET, C_LOC(BUFFER), SIZE, 0)
    CALL ASSERT_EQUALS(INT(SIZE, KIND = C_INT), NBYTES)

    DEALLOCATE(BUFFER)
  END SUBROUTINE

  SUBROUTINE RECV(SOCKET, VALUE)
    TYPE(C_PTR), INTENT(INOUT) :: SOCKET
    INTEGER(KIND = C_INT), INTENT(OUT) :: VALUE

    INTEGER(KIND = C_INT) :: CODE
    INTEGER(KIND = C_INT) :: NBYTES
    INTEGER(KIND = C_SIZE_T) :: SIZE
    CHARACTER(KIND = C_CHAR, LEN = :), TARGET, ALLOCATABLE :: BUFFER
    CHARACTER(KIND = C_CHAR, LEN = :), POINTER :: RANGE

    SIZE = C_SIZEOF(VALUE)
    ALLOCATE(CHARACTER(KIND = C_CHAR, LEN = SIZE) :: BUFFER)

    NBYTES = ZMQ_RECV(SOCKET, C_LOC(BUFFER), SIZE, 0)
    CALL ASSERT_EQUALS(INT(SIZE, KIND = C_INT), NBYTES)

    RANGE => BUFFER(1:SIZE)
    VALUE = TRANSFER(RANGE, VALUE)

    DEALLOCATE(BUFFER)
  END SUBROUTINE

END PROGRAM
